Model Training

ResponsibilityDomain UrdadStandardProfile
{
  ResponsibilityDomain Primitives
  {
    BasicDataType Boolean
    BasicDataType Integer
    BasicDataType FloatingPointNumber
    BasicDataType String
    BasicDataType Date
    BasicDataType URI
  }
  ResponsibilityDomain Storage
  {
    ResponsibilityDomain StorageClient
    {
    }
    DataStructure Entity 
    {
        abstract = true
        has attribute identifier ofType URI 
    }
    
    Exception EntityAlreadyStoredException {}
    Exception EntityNotStoredException {}
    ServiceContract storeEntity
    {
        PreCondition entityNotYetStored 
            raises EntityAlreadyStoredException isRequiredBy (StorageClient)
        Request DataStructure StoreEntityRequest
        {
            has component entityToPersist ofType Entity
        }
        Result DataStructure StoreEntityResult
        {
            has component persistedEntity ofType Entity
        }
    }
    ServiceContract retrieveEntity
    {
        PreCondition entityStored
            raises EntityNotStoredException isRequiredBy (StorageClient)
        Request DataStructure RetrieveEntityRequest
        {
            has attribute entityIdentifier ofType Integer
        }
        Result DataStructure RetrieveEntityResult
        {
            has component retrievedEntity ofType Entity
        }
    }
    ServiceContract removeEntity
    {
        Request DataStructure RemoveEntityRequest
        {
            has attribute entityIdentifier ofType Integer
        }
        Result DataStructure RemoveEntityResult
        {
        }
    }
    ServiceContract updateEntity
    {
        PreCondition entityNotStored
            raises EntityNotStoredException isRequiredBy (StorageClient)
        Request DataStructure UpdateEntityRequest
        {
            has component newEntityState ofType Entity
        }
        Result DataStructure UpdateEntityResult
        {
            has component persistedEntity ofType Entity
        }
    }
    DataStructure StorageQuery
    {
        has attribute queryLanguage ofType URI
        has attribute queryExpression ofType String 
    }
    ServiceContract findEntities
    {
        Request DataStructure FindEntitiesRequest
        {
            has component query ofType StorageQuery
        }
        Result DataStructure FindEntitiesResult
        {
            has many component entity ofType Entity
        }
    }
  }
}

ResponsibilityDomain Training
{
  ResponsibilityDomain Student {}
  ResponsibilityDomain TrainingRegulator {}
  ResponsibilityDomain CreditRegulator {}
  ResponsibilityDomain Shareholder {}
  ResponsibilityDomain Client {}
  
  ResponsibilityDomain TrainingInstitution
  {
    ResponsibilityDomain Academic
    {
      ResponsibilityDomain CourseManagement
      {
        DataStructure Course
        {
            has attribute name ofType String
            has many association coursePrerequisite ofType Course 
        }
      }
      ResponsibilityDomain Assessment
      {
        Exception StudentDoesNotSatisfyPrerequisitesException {}
        ServiceContract checkStudentSatisfiesCoursePrerequisites
        {
          Request DataStructure CheckStudentSatisfiesCoursePrerequisitesRequest
          {
            has component studentIdentification ofType PersonIdentification
            has association presentation ofType Presentation
          }
          Result DataStructure CheckStudentSatisfiesCoursePrerequisitesResult
          {
            abstract = true
            has attribute prerequisitesSatisfied ofType Boolean
          }
        } 
        ResponsibilityDomain Validation
        {
            ServiceContract checkStudentSatisfiesEnrollmentPrerequisites
            { 
                PostCondition ifEnrollmentConflictsWithRosterPrerequisitesNotSatisfied ensures
                    OCL:"result.isKindOf(StudentDoesNotMeetEnrollmentPrerequisites)"
                    isRequiredBy (TrainingInstitution)  
                PostCondition personIdentifierProvided
                    isRequiredBy (TrainingInstitution)
                PostCondition ifNotRegisteredPrerequisitesNotSatisfied
                    isRequiredBy (TrainingInstitution)
                PostCondition ifCourseConditionsNotSatisfiedEnrollmentConditionsNotSatisfied
                    isRequiredBy (TrainingRegulator Student)    
                Request DataStructure CheckStudentSatisfiesEnrollmentPrerequisitesRequest 
                {
                    has component studentIdentification ofType PersonIdentification
                    has association presentationIdentifier ofType Presentation
                }
                Result DataStructure CheckStudentSatisfiesEnrollmentPrerequisitesResult
                {
                    abstract = true
                    has association personIdentifier ofType Person
                    has association presentationIdentifier ofType Presentation
                }
            }
            DataStructure StudentSatisfiesEnrollmentPrerequisites is CheckStudentSatisfiesEnrollmentPrerequisitesResult{}
            DataStructure StudentDoesNotMeetEnrollmentPrerequisites is CheckStudentSatisfiesEnrollmentPrerequisitesResult
            {
                has many attribute reason ofType String
            }
            Service checkStudentSatisfiesEnrollmentPrerequisitesImpl realizes checkStudentSatisfiesEnrollmentPrerequisites
            {
                Request Variable checkStudentSatisfiesEnrollmentPrerequisitesRequest 
                    ofType CheckStudentSatisfiesEnrollmentPrerequisitesRequest
                use checkWhetherEnrollmentConflictsWithStudentRoster toAddress 
                    (ifEnrollmentConflictsWithRosterPrerequisitesNotSatisfied)
                use provideRegistrationDetails toAddress 
                    (personIdentifierProvided ifNotRegisteredPrerequisitesNotSatisfied)                     
                use checkWhetherEnrollmentConflictsWithStudentRoster toAddress (ifEnrollmentConflictsWithRosterPrerequisitesNotSatisfied)                       
                use checkStudentSatisfiesCoursePrerequisites toAddress
                    (ifCourseConditionsNotSatisfiedEnrollmentConditionsNotSatisfied)   
                doSequential
                {
                    create Variable provideRegistrationDetailsRequest ofType ProvideRegistrationDetailsRequest
                    requestService provideRegistrationDetails with provideRegistrationDetailsRequest
                        yields Variable provideRegistrationDetailsResult ofType ProvideRegistrationDetailsResult
                    {
                        on PersonNotRegisteredException
                            doSequential
                            {
                                create Variable checkStudentSatisfiesEnrollmentPrerequisitesResult
                                    ofType StudentDoesNotMeetEnrollmentPrerequisites
                                returnResult checkStudentSatisfiesEnrollmentPrerequisitesResult    
                            }
                    }    
                   Concurrency
                   {
                      doConcurrent doSequential 
                      {
                          create Variable checkStudentSatisfiesCoursePrerequisitesRequest
                              ofType CheckStudentSatisfiesCoursePrerequisitesRequest
                          requestService checkStudentSatisfiesCoursePrerequisites
                              with checkStudentSatisfiesCoursePrerequisitesRequest
                              yields Variable checkStudentSatisfiesCoursePrerequisitesResult
                                  ofType CheckStudentSatisfiesCoursePrerequisitesResult
                      }   
                      doConcurrent doSequential
                      {
                          create Variable checkWhetherEnrollmentConflictsWithStudentRosterRequest ofType
                              CheckWhetherEnrollmentConflictsWithStudentRosterRequest
                          requestService checkWhetherEnrollmentConflictsWithStudentRoster 
                              with checkWhetherEnrollmentConflictsWithStudentRosterResult
                              yields Variable checkWhetherEnrollmentConflictsWithStudentRosterResult
                                  ofType CheckWhetherEnrollmentConflictsWithStudentRosterResult
                      }
                   }
                        
                   choice
                   {
                      if Condition coursePrerequisitesMetAndFitsInRoster
                          OCL:"(checkStudentSatisfiesCoursePrerequisitesResult.prerequisitesSatisfied = true) and 
                            (checkWhetherEnrollmentConflictsWithStudentRosterResult.prerequisitesSatisfied = true)" 
                      doSequential
                      {
                          create Variable checkStudentSatisfiesEnrollmentPrerequisitesResult
                              ofType StudentSatisfiesEnrollmentPrerequisites
                          returnResult checkStudentSatisfiesEnrollmentPrerequisitesResult     
                      }
                      else doSequential
                      {
                        create Variable checkStudentSatisfiesEnrollmentPrerequisitesResult
                          ofType StudentDoesNotMeetEnrollmentPrerequisites
                        if Condition OCL:"checkStudentSatisfiesCoursePrerequisitesResult.prerequisitesSatisfied = false"
                            doSequential
                            {
                                create Variable reason ofType String
                                add Constant "Course prerequisites not satisfied." to 
                                    Query OCL:"checkStudentSatisfiesEnrollmentPrerequisitesResult.reasons"  
                            }             
                        if Condition OCL:"checkWhetherEnrollmentConflictsWithStudentRosterResult.prerequisitesSatisfied = false"
                            doSequential
                            {
                                create Variable reason ofType String
                                add Constant "Enrollment conflicts with student roster." to
                                    Query OCL:"checkStudentSatisfiesEnrollmentPrerequisitesResult.reasons"  
                            }                                 
                        returnResult checkStudentSatisfiesEnrollmentPrerequisitesResult    
                      }  
                   }
                }       
            }
        }
      }
      ResponsibilityDomain StudyGuides
      {
        DataStructure StudyGuide {}
        ServiceContract provideStudyGuide
        {
          PreCondition studentIsRegistered raises PersonNotRegisteredException isRequiredBy (Shareholder)
          Request DataStructure ProvideStudyGuideRequest 
          {
            has association courseIdentifier ofType Course
            has association studentIdentifier ofType Person
          }            
          Result DataStructure ProvideStudyGuideResult 
          {
            has component studyGuide ofType StudyGuide
          }                     
        }
      }
    }
    ResponsibilityDomain ClientRelationshipManagement
    {
	  DataStructure Address
      {
        has many attribute addressLine ofType String
      }
      DataStructure LegalEntity is Entity
      {
        has attribute name ofType String
        has component legalAddress ofType Address
        (Note en-ZA: "Any party you can bill and take to court, e.g. a person or an organization.")
      }
      DataStructure Organization is LegalEntity {}
	  DataStructure Person is LegalEntity {}
      DataStructure Identification is Entity
      {
        abstract = true
        has association issuingOrganization ofType Organization
      } 
      DataStructure CompanyIdentification is Identification
      {
        has attribute companyRegistrationNumber ofType String
      }
      
      DataStructure PersonIdentification is Identification
      {
        abstract = true
        has association person ofType Person
      } 
      DataStructure Passport is PersonIdentification
      {
        has attribute passportNumber ofType String
      }
      DataStructure OrganizationMembershipCard is PersonIdentification
      {
        has attribute cardNumber ofType String
        (Note en-ZA: "For example a student or and employee card")
      }
      Exception PersonNotRegisteredException {}
      ServiceContract provideRegistrationDetails
      {
        Request DataStructure ProvideRegistrationDetailsRequest 
        {
            has component personIdentification ofType PersonIdentification
            has association presentation ofType Presentation
        }
        Result DataStructure ProvideRegistrationDetailsResult 
        {
            has component personDetails ofType Person
        }
      }
    }
    ResponsibilityDomain Finance
    {
      Exception NoInvoiceIssuedForEnrollmentException {}
      Exception FinancialPrerequisitesNotSatisfiedException {}
      ServiceContract issueInvoice
      {
        PreCondition financialPrerequisitesSatisfied raises FinancialPrerequisitesNotSatisfiedException isRequiredBy (Shareholder)
        Request DataStructure IssueInvoiceRequest 
        {
            has component clientIdentification ofType Identification
            has many component invoiceItems ofType InvoiceItem
        }
        Result DataStructure IssueInvoiceResult 
        {
            has component invoice ofType Invoice
        }
      }
      ServiceContract provideInvoiceForEnrollment
      {
        PreCondition invoiceForEnrollmentExists
            raises NoInvoiceIssuedForEnrollmentException isRequiredBy (Finance)
        Request DataStructure  ProvideInvoiceForEnrollmentRequest {}
        Result DataStructure ProvideInvoiceForEnrollmentResult {}
      }
      DataStructure Invoice is Entity
      {
        has attribute invoiceNumber ofType String
        has attribute invoiceDate ofType Date
        has component clientDetails ofType LegalEntity
        has component many invoiceItem ofType InvoiceItem
      }
      DataStructure Chargeable is Entity {}
      DataStructure InvoiceItem
      {
        has attribute cost ofType FloatingPointNumber
        has attribute quantity ofType Integer
        has association chargeableIdentifier ofType Chargeable
      }
    }
    ResponsibilityDomain PresentationManagement
    {
        DataStructure Location is Entity
        {
            has attribute name ofType String
            has component address ofType Address
        }
        DataStructure Presentation is Chargeable 
        {
            has attribute name ofType String
            has attribute startDate ofType Date
            has association location ofType Location
        }
        ServiceContract checkWhetherEnrollmentConflictsWithStudentRoster
        {
            Request DataStructure CheckWhetherEnrollmentConflictsWithStudentRosterRequest{}
            Result DataStructure CheckWhetherEnrollmentConflictsWithStudentRosterResult{}
        }
    }  
    ResponsibilityDomain Enrollments
    {
      ResponsibilityDomain PresentationManagement
      {
        DataStructure PresentationDetails
        {
            has association course ofType Course
        }
        ServiceContract enlistStudentForPresentation
        {
          PreCondition studentIsRegistered
            raises PersonNotRegisteredException isRequiredBy (Shareholder)
          Request DataStructure EnlistStudentForPresentationRequest {}
          Result DataStructure EnlistStudentForPresentationResult {}
        }
      }  
      ResponsibilityDomain ProofOfEnrollments
      {
        DataStructure ProofOfEnrollment is Entity
        {
            has component studentDetails ofType Person
            has component presentationDetails ofType Presentation
        }
        ServiceContract provideProofOfEnrollment
        {
          PreCondition studentIsRegistered
            raises PersonNotRegisteredException isRequiredBy (Shareholder TrainingRegulator)
          Request DataStructure ProvideProofOfEnrollmentRequest {}
          Result DataStructure ProvideProofOfEnrollmentResult {}
        }
      }
      Exception EnrollmentPrerequisitesNotSatisfiedException {}
      ServiceContract enrollForPresentation
      {
        PreCondition financialPrerequisitesMet
            raises FinancialPrerequisitesNotSatisfiedException
            isRequiredBy (Shareholder)    
        PreCondition enrollmentPrerequisitesMet
            raises EnrollmentPrerequisitesNotSatisfiedException
            isRequiredBy (TrainingRegulator Student) 
        PostCondition enrollmentProcessPerformed
            isRequiredBy (Student Client TrainingRegulator)    
        PostCondition invoiceIssued
            isRequiredBy (Shareholder Client CreditRegulator)    
        Request DataStructure EnrollForPresentationRequest 
        {
            has association presentationIdentifier ofType Presentation
            has component studentIdentification ofType PersonIdentification
            has component payerIdentification ofType Identification         
        }
        Result DataStructure EnrollForPresentationResult 
        {
            has component proofOfEnrollment ofType ProofOfEnrollment
            has component invoice ofType Invoice
            has component studyGuide ofType StudyGuide
        }
      }
      Service enrollForPresentationImpl realizes enrollForPresentation
      {
        Request Variable enrollForPresentationRequest ofType EnrollForPresentationRequest
        use checkStudentSatisfiesEnrollmentPrerequisites toAddress (enrollmentPrerequisitesMet)
        use issueInvoice toAddress (financialPrerequisitesSatisfied invoiceIssued) 
        use performEnrollment toAddress ( invoiceIssued )
        doSequential
        {
            create Variable checkStudentSatisfiesEnrollmentPrerequisitesRequest 
                ofType CheckStudentSatisfiesEnrollmentPrerequisitesRequest
                
            set Query studentDetailsSource OCL:"enrollForPresentationRequest.studentIdentification" equalTo 
                Query studentDetailsTarget OCL:"checkEnrollmentPrerequisitesRequest.studentIdentification"
            set Query presentationIdentifierSource OCL:"enrollForPresentationRequest.presentationIdentifier" equalTo
                Query presentationIdentifierTarget OCL:"checkEnrollmentPrerequisitesRequest.presentationIdentifier"     
            requestService checkStudentSatisfiesEnrollmentPrerequisites 
                with checkStudentSatisfiesEnrollmentPrerequisitesRequest 
                yields Variable checkStudentSatisfiesEnrollmentPrerequisitesResult
                    ofType CheckStudentSatisfiesEnrollmentPrerequisitesResult
            choice
            {
                if Condition enrollmentMeetsPrerequisitesMet 
                    OCL:"checkStudentSatisfiesEnrollmentPrerequisitesResult.enrollmentPrerequisitesMet = true"
                    doSequential
                    {
                        create Variable issueInvoiceRequest ofType IssueInvoiceRequest
                        set Query clientIdentificationSource OCL: "enrollForPresentationRequest.payerIdentification" equalTo 
                            Query clientIdentificationTarget OCL:"issueInvoiceRequest.clientIdentification"
                        create Variable invoiceItem ofType InvoiceItem    
			            set Query presentationIdentifierSource OCL:"enrollForPresentationRequest.presentationIdentifier" equalTo
			                Query presentationIdentifierTarget OCL:"invoiceItem.chargeableIdentifier"
			            add Query invoiceItemSource OCL:"invoiceItem" to
			                Query invoiceItemTarget OCL:"issueInvoiceRequest.invoiceItems"         
                        requestService issueInvoice with issueInvoiceRequest 
                            yields Variable issueInvoiceResult ofType IssueInvoiceResult
                        {
                            on FinancialPrerequisitesNotSatisfiedException 
                                raiseException FinancialPrerequisitesNotSatisfiedException
                        }
                        create Variable enrollRequest ofType PerformEnrollmentRequest 
                        set Query personIdentifierSource OCL:"checkStudentSatisfiesEnrollmentPrerequisitesResult.personIdentifier" equalTo
                            Query personIdentierTarget OCL:"performEnrollmentRequest.personIdentifier"
                        set Query presentationIdentifierSource OCL:"enrollForPresentationRequest.presentationIdentifier" equalTo
                            Query presentationIdentifierTarget OCL:"performEnrollmentRequest.presentationIdentifier"    
                        requestService performEnrollment with enrollRequest 
                            yields Variable performEnrollmentResult ofType PerformEnrollmentResult
                        create Variable enrollForPresentationResult ofType EnrollForPresentationResult
                        set Query invoiceSource OCL:"issueInvoiceResult.invoice" equalTo
                            Query invoiceTarget OCL:"enrollForPresentationResult.invoice"
                        set Query enrollmentConfirmationSource OCL:"performEnrollmentResult.enrollmentConfirmation" equalTo
                            Query enrollmentConfirmationTarget OCL:"enrollForPresentationResult.enrollmentConfirmation"
                        set Query studyGuidesSource OCL:"performEnrollmentResult.studyGuide" equalTo
                            Query studyGuidesTarget OCL:"enrollForPresentationResult.studyGuide"    
                        returnResult  enrollForPresentationResult
                    }
                else
                    raiseException EnrollmentPrerequisitesNotSatisfiedException
                        
            }
        }  
      }
      
      ServiceContract performEnrollment
      {
        Request DataStructure PerformEnrollmentRequest 
        {
            has association presentationIdentifier ofType Presentation
            has association personIdentifier ofType Person
        }
        Result DataStructure PerformEnrollmentResult 
        {
            has component enrollmentConfirmation ofType ProofOfEnrollment
            has component studyGuide ofType StudyGuide
        }
      }
    } 
  }
}  
